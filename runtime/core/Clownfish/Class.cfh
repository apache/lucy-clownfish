/* Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

parcel Clownfish;

/** Class.
 *
 * Classes, which are the first element in any Clownfish object, are actually
 * objects themselves.  (Their first element is a Class which describes the
 * behavior of Classes.)
 */

class Clownfish::Class inherits Clownfish::Obj {

    Class              *parent;
    String             *name;
    String             *name_internal;
    uint32_t            flags;
    int32_t             parcel_id;
    size_t              obj_alloc_size;
    size_t              class_alloc_size;
    Method            **methods;
    cfish_method_t[1]   vtable; /* flexible array */

    inert LockFreeRegistry *registry;
    inert size_t offset_of_parent;

    inert void
    bootstrap(const cfish_ClassSpec *specs, size_t num_specs);

    /** Return a singleton.  If a Class can be found in the registry based on
     * the supplied class name, it will be returned.  Otherwise, a new Class
     * will be created using [parent] as a base.
     *
     * If [parent] is NULL, an attempt will be made to find it using
     * [](cfish:.find_parent_class).  If the attempt fails, an error will
     * result.
     */
    inert Class*
    singleton(String *class_name, Class *parent);

    /** Register a class, so that it can be retrieved by class name.
     *
     * TODO: Move this functionality to some kind of class loader.
     *
     * @return true on success, false if the class was already registered.
     */
    inert bool
    add_to_registry(Class *klass);

    inert bool
    add_alias_to_registry(Class *klass, const char *alias_ptr,
                          size_t alias_len);

    /** Initialize the registry.
     */
    inert void
    init_registry();

    /** Tell the host about the new class.
     */
    inert void
    register_with_host(Class *klass, Class *parent);

    /** Find a registered class.  May return NULL if the class is not
     * registered.
     */
    inert nullable Class*
    fetch_class(String *class_name);

    /** Given a class name, return the name of a parent class which descends
     * from Clownfish::Obj, or NULL if such a class can't be found.
     */
    inert nullable String*
    find_parent_class(String *class_name);

    /** List all of the methods defined directly within a host subclass.
     */
    inert incremented VArray*
    fresh_host_methods(String *class_name);

    /** Replace a function pointer in the Class's vtable.
     */
    void
    Override(Class *self, cfish_method_t method_ptr, size_t offset);

    /** Create an empty object of the type defined by the Class: allocate,
     * assign its class and give it an initial refcount of 1.  The caller is
     * responsible for initialization.
     */
    incremented Obj*
    Make_Obj(Class *self);

    /** Take a raw memory allocation which is presumed to be of adequate size,
     * assign its class and give it an initial refcount of 1.
     */
    incremented Obj*
    Init_Obj(Class *self, void *allocation);

    /** Create a new object to go with the supplied host object.
     */
    Obj*
    Foster_Obj(Class *self, void *host_obj);

    void
    Add_Host_Method_Alias(Class *self, const char *alias,
                          const char *meth_name);

    void
    Exclude_Host_Method(Class *self, const char *meth_name);

    String*
    Get_Name(Class *self);

    Class*
    Get_Parent(Class *self);

    size_t
    Get_Obj_Alloc_Size(Class *self);

    /** Return novel methods of the class.
     */
    VArray*
    Get_Methods(Class *self);

    public incremented Class*
    Clone(Class *self);

    public void
    Destroy(Class *self);
}


